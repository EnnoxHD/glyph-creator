plugins {
   id 'java'
   id 'application'
   id 'org.openjfx.javafxplugin' version '0.0.9'
}

java {
   modularity.inferModulePath = true
   toolchain {
      languageVersion = JavaLanguageVersion.of(15)
   }
}

application {
	mainModule = 'com.github.ennoxhd.glyphcreator'
	mainClass  = 'com.github.ennoxhd.glyphcreator.app.GlyphCreatorApp'
}

javafx {
    version = '15'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
}

task debug (type: Exec) {
   group = 'application'
   description = 'Runs this project as a JVM application with debugging enabled (add program arguments with -Pargs="...")'

   def baseCommand = 'run --debug-jvm'
   if(project.hasProperty('args')) {
      baseCommand += ' --args="' + project.getProperty('args') + '"'
   }
   if (System.properties['os.name'].toLowerCase().contains('windows')) {
      commandLine = ['cmd', '/c', 'gradlew.bat ' + baseCommand]
   } else {
      commandLine = ['sh', 'gradlew ' + baseCommand]
   }
}
